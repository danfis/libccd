find_program(SPHINX_EXECUTABLE NAMES sphinx-build sphinx-build2)

if(NOT SPHINX_EXECUTABLE)
  message(FATAL_ERROR "Could NOT find required executable sphinx-build")
endif()

add_custom_target(doc ALL)

set(CCDDBL_DOCTREE_DIR "${CMAKE_CURRENT_BINARY_DIR}/.doctrees")
set(CCDDBL_HTML_OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/html")

add_custom_target(html COMMAND
  "${SPHINX_EXECUTABLE}" -b html -d "${CCDDBL_DOCTREE_DIR}" -q
  "${CMAKE_CURRENT_SOURCE_DIR}" "${CCDDBL_HTML_OUTPUT_DIR}")
add_dependencies(doc html)

install(DIRECTORY "${CCDDBL_HTML_OUTPUT_DIR}"
  DESTINATION "${CMAKE_INSTALL_DATAROOTDIR}/doc/ccddbl")

set(CCDDBL_DOC_ADDITIONAL_MAKE_CLEAN_FILES
  "${CCDDBL_DOCTREE_DIR}"
  "${CCDDBL_HTML_OUTPUT_DIR}")

if(NOT WIN32)
  set(CCDDBL_MAN_OUTPUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/man")

  add_custom_target(man COMMAND
    "${SPHINX_EXECUTABLE}" -b man -d "${CCDDBL_DOCTREE_DIR}" -q
    "${CMAKE_CURRENT_SOURCE_DIR}" "${CCDDBL_MAN_OUTPUT_DIR}")
  add_dependencies(doc man)

  install(DIRECTORY "${CCDDBL_MAN_OUTPUT_DIR}/"
    DESTINATION "${CMAKE_INSTALL_MANDIR}/man1")

  list(APPEND CCDDBL_DOC_ADDITIONAL_MAKE_CLEAN_FILES "${CCDDBL_MAN_OUTPUT_DIR}")
endif()

set_directory_properties(PROPERTIES
  ADDITIONAL_MAKE_CLEAN_FILES ${CCDDBL_DOC_ADDITIONAL_MAKE_CLEAN_FILES})
